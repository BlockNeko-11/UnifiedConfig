plugins {
    id "idea"
    id "java-library"
    id "maven-publish"
}

base {
    archivesName = rootProject.archives_base_name
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.artifact_version

    repositories {
        maven {
            name = "central"
            url = "https://repo1.maven.org/maven2/"
        }
        mavenCentral()
    }
}

subprojects {
    apply plugin: "idea"
    apply plugin: "java-library"
    apply plugin: "maven-publish"

    base {
        archivesName = rootProject.archives_base_name + "-" + project.name
    }

    dependencies {
        compileOnly "org.jetbrains:annotations:26.1.0"
        annotationProcessor(compileOnly("org.projectlombok:lombok:1.18.36"))

        testImplementation platform("org.junit:junit-bom:5.11.4")
        testImplementation "org.junit.jupiter:junit-jupiter"
        testAnnotationProcessor(testCompileOnly("org.projectlombok:lombok:1.18.36"))
    }

    test {
        useJUnitPlatform()

        testLogging {
            exceptionFormat = "full"
            events "passed", "skipped", "failed"
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }

    compileJava.dependsOn clean

    publishing {
        publications {
            create("MavenJava", MavenPublication) {
                from components.java
                artifactId = rootProject.archives_base_name + "-" + project.name
            }
        }

        repositories {
            mavenLocal()
            maven {
                name = "Local"
                url = rootProject.buildDir.toPath().resolve("repo").toAbsolutePath().toString()
            }
        }
    }
}
